plugins {
	id "java"
	id "idea"
	id "jaci.openrio.gradle.GradleRIO" version "2018.01.07"
}

def TEAM = 2713
def ROBOT_CLASS = "org.usfirst.frc.team2713.robot.Robot"

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }

  project {
    jdkName = '1.8'
    languageLevel = '1.8'

    ipr.withXml { xmlFile ->
      // enable 'Annotation Processors'
      xmlFile.asNode().component.find{ it.@name == 'CompilerConfiguration' }['annotationProcessing'][0].replaceNode {
        annotationProcessing {
          profile(default: true, name: 'Default', useClasspath: 'true', enabled: true)
        }
      }
    }
  }
}

deploy {
  targets {
    target("roborio", jaci.openrio.gradle.frc.RoboRIO) {
      team = TEAM
    }
  }
  artifacts {
    artifact('frcJava', jaci.openrio.gradle.frc.FRCJavaArtifact) {
      targets << "roborio"
    }
  }
}

repositories {
	mavenCentral()
}

dependencies {
	compile wpilib()
  compile ctre() // Talon SRX
  compile navx()
  compile openrio.powerup.matchData()

  compile fileTree(dir: 'libs/Pathfinder', include: '*.jar') // https://tinyurl.com/JaciPathfinderFRC

  compileOnly 'org.projectlombok:lombok:1.16.18'

	testCompile 'junit:junit:4.12'
}

jar {
  from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.4'
}
